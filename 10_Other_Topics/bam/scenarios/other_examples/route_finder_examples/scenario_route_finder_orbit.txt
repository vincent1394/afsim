# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************


define_path_variable CASE scenario_route_finder_orbit

event_pipe file output/$(CASE).aer end_event_pipe

end_time 120 min

platform target WSF_PLATFORM
   icon B-747
   side red
   add mover WSF_AIR_MOVER end_mover
   route
      position 00.31532n 00.73940e altitude 10000 ft msl
         speed 50 knots
      position 0.315n 1.2e
      position 1.0n 1.2e 
   end_route
end_platform

script void DrawRoute(WsfRoute aRoute)
   if (aRoute.IsValid())
   {
      WsfDraw mDraw = WsfDraw();
      mDraw.SetDuration(60);
      mDraw.SetColor(1,1,1);
      mDraw.SetLineSize(1);
      mDraw.BeginPolyline();
      for (int i=0; i<aRoute.Size(); i=i+1)
      {
         mDraw.Vertex(aRoute.Waypoint(i).Location());
      }
      mDraw.End();
   }
end_script

platform player-1 WSF_PLATFORM
   side blue
   icon F-18E
   add mover WSF_AIR_MOVER update_interval 1 sec end_mover
   route
      position 00.32462n 02.0e altitude 10000 ft msl speed 250 m/s
      position 00.32462n 01.0e altitude 10000 ft msl speed 250 m/s
   end_route

   script_variables
      WsfRouteFinder finder = WsfRouteFinder();
   end_script_variables

   update_interval 1 sec
   execute at_interval_of 10 sec          
      finder.SetMaxArcLength(4000); // a little over 2 nm (in meters)
      WsfGeoPoint src = PLATFORM.Location();
      WsfPlatform tgt = WsfSimulation.FindPlatform("target"); 
      
      WsfGeoPoint tempLoc = tgt.Location(); 
      double locDir = PLATFORM.TrueBearingTo(tgt)-1.0;
      tempLoc.Extrapolate(locDir, 20*MATH.M_PER_NM()); 
      
      finder.ClearAvoidances(); 
      finder.Avoid(tgt.Location(), 1852*20);  
      
      WsfRoute path = finder.Route(TIME_NOW, src, tempLoc, 250); // 250 met/sec ~500 knots
      
      writeln("path: ");
      for(int i=0; i<path.Size(); i=i+1)
      {
         WsfWaypoint pt = path.Waypoint(i);
         writeln(pt.Location().ToString());
      }

      writeln_d(PLATFORM.Name(), " given path, size: ", path.Size());
      PLATFORM.SetRoute(path);
      finder.DrawAvoidances(1, Vec3.Construct(0.4, 0.4, 0.4));
      DrawRoute(path);
   end_execute
end_platform

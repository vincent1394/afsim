# ****************************************************************************
# CUI
#
# The Advanced Framework for Simulation, Integration, and Modeling (AFSIM)
#
# The use, dissemination or disclosure of data in this file is subject to
# limitation or restriction. See accompanying README and LICENSE for details.
# ****************************************************************************

# File generated by Wizard 2.8.0 on Feb 21, 2021.

processor SINGLE_LARGE_SAM_TACTICS WSF_TASK_PROCESSOR

   script_variables
      double launchRange = 20.0 * MATH.M_PER_NM();
      double salvoSize   = 2;
      string weaponName  = "sam";
   end_script_variables
   
   script bool CanEngage()
      bool canEngage = false;
      WsfWeapon sam = PLATFORM.Weapon(weaponName);
      if (sam.IsValid() &&
          sam.QuantityRemaining() >= salvoSize &&
          TRACK.Target().IsValid() &&
          PLATFORM.GroundRangeTo(TRACK) < launchRange &&
          WeaponsActiveFor(TRACK.TrackId()) < 1)
       {
          canEngage = true;
       }
       return canEngage;
   end_script
   
/////////////////////////////// STATE MACHINE ///////////////////////////////
   
   evaluation_interval DETECTED 10 sec
   state DETECTED
      next_state IGNORE
         return (TRACK.IFF_Friend());
      end_next_state
      
      next_state ENGAGE
         return CanEngage();
      end_next_state
   end_state
   
   evaluation_interval ENGAGE 10 sec
   state ENGAGE
      next_state WAIT
         return Fire(TRACK, "FIRE", weaponName, salvoSize, PLATFORM);
      end_next_state
   end_state
   
   evaluation_interval WAIT 10 sec
   state WAIT
      next_state ENGAGE
         return CanEngage();
      end_next_state
   end_state
   
   evaluation_interval IGNORE 24 hr
   state IGNORE
   end_state
end_processor
